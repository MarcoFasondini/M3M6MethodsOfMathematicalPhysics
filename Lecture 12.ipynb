{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "using Plots, ComplexPhasePortrait, ApproxFun, SingularIntegralEquations\n",
    "gr();"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# M3M6: Methods of Mathematical Physics\n",
    "\n",
    "$$\n",
    "\\def\\dashint{{\\int\\!\\!\\!\\!\\!\\!-\\,}}\n",
    "\\def\\infdashint{\\dashint_{\\!\\!\\!-\\infty}^{\\,\\infty}}\n",
    "\\def\\D{\\,{\\rm d}}\n",
    "\\def\\dx{\\D x}\n",
    "\\def\\dt{\\D t}\n",
    "\\def\\C{{\\mathbb C}}\n",
    "\\def\\CC{{\\cal C}}\n",
    "\\def\\HH{{\\cal H}}\n",
    "\\def\\I{{\\rm i}}\n",
    "\\def\\qqfor{\\qquad\\hbox{for}\\qquad}\n",
    "$$\n",
    "\n",
    "Dr. Sheehan Olver\n",
    "<br>\n",
    "s.olver@imperial.ac.uk\n",
    "\n",
    "Office Hours: 3-4pm Mondays, Huxley 6M40\n",
    "<br>\n",
    "Website: https://github.com/dlfivefifty/M3M6LectureNotes\n",
    "\n",
    "# Lecture 12: Ideal fluid flow\n",
    "\n",
    "Understanding branch cuts and Cauchy transforms allows us to systematically solve equations involving Laplace equation. A classic example is Ideal fluid flow. Consider the case of uniform flow with angle $\\theta$ around an infinitesimally small plate on $[-1,1]$. We can model this as\n",
    "$$\n",
    "\\begin{align*}\n",
    "v(x,y) &\\sim y \\cos \\theta - x \\sin \\theta  \\\\\n",
    "v_{xx} + v_{yy} &= 0 \\\\\n",
    "v(x,0) & = 0 \\qqfor -1 < x <1\n",
    "\\end{align*}\n",
    "$$\n",
    "Using the techniques we developed in the last few lectures, we obtain a nice, simple, closed form expression as the imaginary part of an analytic function:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "μ = z -> -im*(sqrt(z-1)sqrt(z+1) - z)\n",
    "Φ = (θ,z) -> exp(-im*θ)*z + sin(θ)μ(z)\n",
    "u = (θ,x,y) -> imag(Φ(θ, x + im*y))\n",
    "xx = yy = linspace(-3.,3.,500)\n",
    "contour(xx, yy, u.(1.3,xx',yy); nlevels = 100)\n",
    "plot!(Segment(-1.,1.); color=:black, label=\"obstacle\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We divide this task into stages:\n",
    "\n",
    "1. Rephrasing as a complex-analytical problem: $v(x,y)$ to $\\phi(z)$\n",
    "2. Reduction to inverting a Hilbert transform: $\\phi(z)$ to $w(x)$\n",
    "3. Calculating the inverse Hilbert transform: Finding $w(x)$\n",
    "4. Calculating its Cauchy transform: $w(x)$ to $\\phi(z)$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "\n",
    "### 1. Real and imaginary parts of analytic functions satisfy Laplace's equation\n",
    "\n",
    "The real and imaginary parts of an analytic function satisfy Laplace's equation: that is  if $\\phi(z) = \\phi(x + \\I y) = u(x,y) + \\I v(x,y)$ where $u$ and $v$ are the real/imaginary parts, then \n",
    "$$\n",
    " u_{xx} + u_{yy}= 0 \\\\\n",
    " v_{xx} + v_{yy} = 0\n",
    "$$\n",
    "To see this, note that the complex-derivative of $\\phi(z)$ can be written in terms of two different partial derivatives:\n",
    "$$\n",
    "    \\phi'(z) = \\lim_{h \\rightarrow 0} {\\phi(z+h) - \\phi(z) \\over h} = \\lim_{h \\rightarrow 0} {u(x+h,y)-u(x,y) + \\I (v(x+h,y)-v(x,y)) \\over h} = u_x + \\I v_x \\\\\n",
    "    \\phi'(z) = \\lim_{h \\rightarrow 0} {\\phi(z+\\I h) - \\phi(z) \\over \\I h} = \\lim_{h \\rightarrow 0} {u(x,y+h)-u(x,y) + \\I (v(x,y+h)-v(x,y)) \\over \\I h} = - \\I u_y + v_y\n",
    "$$\n",
    "Taking a second derivative we get two equations:\n",
    "$$\n",
    "    \\phi'(z) = u_{xx} + \\I v_{xx} = -u_{xx} -\\I v_{yy} \n",
    "$$\n",
    "which implies $u_{xx} + u_{yy} = 0$ and $v_{xx} + v_{yy} = 0$.\n",
    "\n",
    "\n",
    "#### Reduce PDE to the Hilbert transform of an unknown function\n",
    "\n",
    "Therefore we can rewrite the ideal fluid flow equation as a problem of calculating $\\phi(z) = u(x,y) + \\I v(x,y)$ whose imaginary part is the solution too the ideal fluid flow PDE (we don't use the real part $u$). That is, we want to find analytic $\\phi(z)$ that satisfies\n",
    "\\begin{align*}\n",
    "    \\phi(z) &\\sim e^{-\\I \\theta} z \\\\\n",
    "    \\Im \\phi(x) &= 0  \\qquad\\hbox{for}\\qquad -1 < x < 1\n",
    "\\end{align*}\n",
    "\n",
    "\n",
    "Write\n",
    "$$\n",
    "\\phi(z) = e^{-\\I \\theta} z + c + \\CC_{[-1,1]} f(z)\n",
    "$$\n",
    "for an as-of-yet unknown function $f$ and $C$ an unknown constant, we have that\n",
    "$$\n",
    "0 = \\Im \\phi(x) = -x \\sin \\theta + \\Im c + \\Im \\CC_{[-1,1]}^+ f(x) = -x \\sin \\theta + \\Im c -{1 \\over 2} \\HH f(x)\n",
    "$$\n",
    "In this example, we can take $c = 0$. Therefore, we want to solve\n",
    "$$\n",
    "\\HH f(x) = - 2 x  \\sin \\theta\n",
    "$$\n",
    "for $f$.  But recall our example from above:\n",
    "$$\n",
    "{\\cal H}\\left[{\\sqrt{1-\\diamond^2}}\\right](x) = -x \n",
    "$$\n",
    "and\n",
    "$$\n",
    "\\CC\\left[{\\sqrt{1-\\diamond^2}}\\right](z) = {\\sqrt{z-1} \\sqrt{z+1} -z \\over 2 \\I}\n",
    "$$\n",
    "\n",
    "Therefore, $f(x) = 2 \\sin \\sqrt{1-\\diamond^2}$, which implies\n",
    "$$\n",
    "\\CC f(z) = - \\I \\sin \\theta (\\sqrt{z-1} \\sqrt{z+1} -z)\n",
    "$$\n",
    "which means\n",
    "$$\n",
    "\\phi(z) = e^{-\\I \\theta} z - \\I \\sin \\theta (\\sqrt{z-1} \\sqrt{z+1} -z).\n",
    "$$\n",
    "\n",
    "\n",
    "#### Numerical example of two intervals\n",
    "\n",
    "We note that for obstacles on the real line, represented by a contour $\\Gamma$, the problem of ideal fluid flow around $\\gamma$ is still reducible to solving the singular integral equation\n",
    "$$\n",
    "\\HH_\\Gamma f(x) = - 2 x  \\sin \\theta\n",
    "$$\n",
    "Even when not solvable exactly, one can solve it numerically:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "a = 0.3\n",
    "θ = 1.3\n",
    "Γ = Segment(-1,-a) ∪ Segment(a, 1)\n",
    "\n",
    "x = Fun(Γ)\n",
    "sp = PiecewiseSpace(JacobiWeight.(0.5,0.5,components(Γ))...)\n",
    "H = Hilbert(sp)\n",
    "\n",
    "o₁ = Fun(x -> -1 ≤ x ≤ -a ? 1 : 0, Γ )\n",
    "o₂ = Fun(x -> a ≤ x ≤ 1 ? 1 : 0, Γ )\n",
    "\n",
    "a, b, f = [o₁ o₂ H] \\ [-2x*sin(θ)]\n",
    "\n",
    "Φ = (θ,z) -> exp(-im*θ)*z + cauchy(f, z)\n",
    "v = (θ,x,y) -> imag(Φ(θ, x + im*y))\n",
    "\n",
    "xx = yy = linspace(-3.,3.,500)\n",
    "contour(xx, yy, v.(θ, xx',yy); nlevels = 100)\n",
    "plot!(Γ; color=:black, label=\"obstacle\")"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.6.5-pre",
   "language": "julia",
   "name": "julia-0.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.6.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
